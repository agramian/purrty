apply plugin: 'com.android.library'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 1
        versionName '0.0.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(":shapedrawablewithborder")
    implementation project(":util")
    api project(":coachmarks")
    api project(":arrow")
    api project(":bottombar")
}

/**
 * From here down referenced: https://github.com/firebase/FirebaseUI-Android/blob/master/build.gradle
 */

/**
 * Returns list of tasks names that prepareArtfacts should depend on.
 */
def prepareArtifactsTasks() {
    return submodules.collect { name ->
        ":${name}:prepareArtifacts"
    }.toArray()
}

/**
 * Returns list of publish tasks that monolith publish tasks should depend on.
 * @param repo relevant repository, such as 'MavenLocal' or 'CustomLocalRepository'
 */
def publishToRepoTasks(repo) {
    return submodules.collect { name ->
        // Ex: name = database, repo = MavenLocal
        // Return: :database:publishDatabaseLibraryPublicationToMavenLocal
        ":${name}:publish${name.capitalize()}LibraryPublicationTo${repo}"
    }.toArray()
}

/**
 * Returns a list of task names to upload modules to bintray.
 */
def bintrayUploadTasks() {
    return submodules.collect { name ->
        ":${name}:bintrayUpload"
    }.toArray()
}

/**
 * Returns a list of tasks names to test submodules.
 */
def testTasks() {
    return submodules.collect { name ->
        ":${name}:testDebugUnitTest"
    }.toArray()
}

/**
 * Publish all artifacts to the maven local repository.
 */
task publishAllToMavenLocal(dependsOn: [
        'publishMonolithLibraryPublicationToMavenLocal',
        publishToRepoTasks('MavenLocal')]) {

}

/**
 * Publish all artifacts to the customLocal repository.
 */
task publishAllToCustomLocal(dependsOn: [
        'publishMonolithLibraryPublicationToCustomLocalRepository',
        publishToRepoTasks('CustomLocalRepository')]) {

}

/**
 * Upload all artifacts to bintray.
 */
task bintrayUploadAll(dependsOn: [
        'prepareArtifacts',
        'bintrayUpload',
        bintrayUploadTasks()]) {

}

/**
 * Test all submodules.
 */
task testAll(dependsOn: [testTasks()]) {
}

afterEvaluate {
    /**
     * Prepare artifacts for this and all sub-projects.
     */
    task prepareArtifacts(dependsOn: [
            javadocJar,
            sourcesJar,
            assembleRelease,
            prepareArtifactsTasks(),
            "generatePomFileForMonolithLibraryPublication"]) {
    }
}
