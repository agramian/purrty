// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'constants.gradle'

buildscript {
    repositories {
        jcenter()
        google()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenLocal()
        mavenCentral()
    }
}

//Skip Javadoc generation for Java 1.8 as it breaks build
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

/**
 * Configure common tasks on all the submodules
 *
 * From here down referenced: https://github.com/firebase/FirebaseUI-Android/blob/master/build.gradle
 */
allprojects { project ->

    // Get constants, this is where we store things
    // like the list of submodules or the version
    project.apply from: "$rootDir/constants.gradle"

    def isLibrary = 'library' == project.name
    def isSubmodule = submodules.contains(project.name)

    if (isSubmodule || isLibrary) {

        // So that we can resolve 'android' variable
        project.apply plugin: 'com.android.library'
        android {
            compileSdkVersion compileSdk
            buildToolsVersion buildTools
        }

        // Task to generate sources JAR
        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        // Task to generate javadoc
        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += configurations.compile
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        // Task to generate javadoc JAR
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        // Define base name for archives
        // Ex: purrty-ui-util
        archivesBaseName = isSubmodule ? "${baseName}-${project.name}" : baseName

        // Use tasks above to define artifacts
        artifacts {
            archives javadocJar
            archives sourcesJar
        }

        // So that we can define publication
        project.apply plugin: 'maven-publish'

        publishing {
            // By passing -Pcustom_local=/some/path and running the
            // publishLibraryPublicationToCustomLocalRepository task you can publish this library to a
            // custom maven repository location on your machine.
            repositories {
                maven {
                    name 'CustomLocal'
                    url uri(project.hasProperty('custom_local') ? project.getProperty('custom_local') : '/tmp/')
                }
            }

            repositories {
                maven {
                    name 'BuildLocal'
                    url "$buildDir/repo"
                }
            }
        }
    }

    // POM to meet maven specs
    if (isLibrary || isSubmodule) {
        def targetName = isLibrary ? 'monolithLibrary' : "${project.name}Library"

        publishing {
            publications {
                "${targetName}"(MavenPublication) {
                    pom.withXml {
                        // Name
                        asNode().appendNode('name', artifactId)

                        // Description
                        asNode().appendNode('description', descriptionLong)

                        // Organization
                        def organization = asNode().appendNode('organization')
                        organization.appendNode('name', organizationName)
                        organization.appendNode('url', repoUrl)

                        // URL
                        asNode().appendNode('url', repoUrl)

                        // SCM
                        def scm = asNode().appendNode('scm')
                        scm.appendNode('connection', scmUrl)
                        scm.appendNode('developerConnection', scmUrl)
                        scm.appendNode('url', repoUrl)
                        scm.appendNode('tag', 'HEAD')

                        // Developers
                        def developer = asNode().appendNode('developers').appendNode('developer')
                        developer.appendNode('id', developerId)
                        developer.appendNode('email', developerName)
                        developer.appendNode('organization', organizationName)
                        developer.appendNode('organizationUrl', organizationUrl)
                        def roles = developer.appendNode('roles')
                        roles.appendNode('role', 'Project-Administrator')
                        roles.appendNode('role', 'Developer')
                        developer.appendNode('timezone', '-8')

                        // Licenses
                        def license = asNode().appendNode('licenses').appendNode('license')
                        license.appendNode('name', licenseName)
                        license.appendNode('url', licenseUrl)
                    }
                }
            }
        }
    }

    if (isSubmodule) {
        publishing {

            // Define a publication for each submodule
            publications {

                // Ex: authLibrary(MavenPublication)
                "${project.name}Library"(MavenPublication) {
                    groupId group
                    artifactId archivesBaseName
                    version version

                    // Release AAR, Sources, and JavaDoc
                    artifact "$buildDir/outputs/aar/$archivesBaseName-release.aar"
                    artifact javadocJar
                    artifact sourcesJar

                    pom.withXml {
                        def dependenciesNode = asNode().getAt("dependencies")[0]
                        if (dependenciesNode == null) {
                            dependenciesNode = asNode().appendNode("dependencies")
                        }

                        // Add all that are 'compile'
                        configurations.compile.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', (it.group == group && submodules.contains(it.name)) ? "${baseName}-${it.name}" : it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }

    if (isLibrary) {

        // Define the monolith publication
        publishing {

            publications {
                monolithLibrary(MavenPublication) {
                    groupId group
                    artifactId archivesBaseName
                    version version

                    artifact "$buildDir/outputs/aar/$archivesBaseName-release.aar"
                    artifact javadocJar
                    artifact sourcesJar

                    // Monolith is just a POM that depends on the others
                    pom.withXml {
                        def dependenciesNode = asNode().getAt("dependencies")[0]
                        if (dependenciesNode == null) {
                            dependenciesNode = asNode().appendNode("dependencies")
                        }

                        // Add a maven dependency on each submodule
                        submodules.each { module ->
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', group)
                            dependencyNode.appendNode('artifactId', "${baseName}-${module}")
                            dependencyNode.appendNode('version', version)
                        }
                    }
                }
            }
        }
    }

    // Bintray Configuration (applies to submodule and the monolith)
    if (isSubmodule || isLibrary) {
        project.apply plugin: 'com.jfrog.bintray'

        def archivesBaseName = isLibrary ? baseName : "${baseName}-${project.name}"
        def publicationName = isLibrary ? 'monolithLibrary' : "${project.name}Library"
        def pomLoc = isLibrary ? "$buildDir/publications/monolithLibrary/pom-default.xml" : "$buildDir/publications/${project.name}Library/pom-default.xml"

        group = project.ext.group
        version = project.ext.version

        def versionName = project.ext.version

        bintray {
            user = hasProperty('BINTRAY_USER') ? getProperty('BINTRAY_USER') : System.getenv('BINTRAY_USER')
            key = hasProperty('BINTRAY_API_KEY') ? getProperty('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
            publications = [publicationName]

            filesSpec {
                from pomLoc
                into "com/$grpPkg/$archivesBaseName/$versionName/"
                rename { String fileName ->
                    "${archivesBaseName}-${version}.pom"
                }
            }

            configurations = ['archives']

            pkg {
                repo = repoName
                name = archivesBaseName
                userOrg = organizationName
                licenses = [licenseName]
                vcsUrl = gitUrl
                websiteUrl = repoUrl

                version {
                    name = versionName
                }
            }
        }
    }
}

allprojects {
    afterEvaluate { project ->
        def isSubmodule = submodules.contains(project.name)

        if (isSubmodule) {
            // Only applies to submodules, not the library module
            def pomTask = "generatePomFileFor${project.name.capitalize()}LibraryPublication"

            // Convenience task to prepare everything we need for releases
            task prepareArtifacts(dependsOn: [javadocJar, sourcesJar, assembleRelease, pomTask]) {}
        }

        if (project.name == 'library' || isSubmodule) {
            // make sure the bintrayUpload task executes after prepareArtifacts
            // is complete so that all the files are available when uploading
            bintrayUpload.dependsOn 'prepareArtifacts', ':library:prepareArtifacts'
        }
    }
}
